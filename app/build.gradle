apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'


android {
    def versionPropsFile = file('../version.properties')
    def buildNumber

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        buildNumber = versionProps['BUILD_NUMBER'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    project.ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            buildNumber = versionProps['BUILD_NUMBER'].toInteger() + 1
            versionProps['BUILD_NUMBER'] = buildNumber.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    gradle.taskGraph.whenReady {taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {  /* when run debug task */
        } else if (taskGraph.hasTask(assembleRelease)) { /* when run release task */
            autoIncrementBuildNumber()
        }
    }
    compileSdkVersion 25
    defaultConfig {
        applicationId "com.micro.microvideo"
        minSdkVersion 16
        targetSdkVersion 25
        buildToolsVersion "25.0.2"
        versionCode buildNumber
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            keyAlias 'hboxs'
            keyPassword 'android'
            storeFile file('../hboxs.jks')
            storePassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->

        if (variant.buildType.name.equals('release')) {
            variant.outputs.each { output ->
                def appName = 'microVideo'
                def oldFile = output.outputFile
                def releaseApkName

                releaseApkName = appName + '-' + variant.versionName + '.' + variant.versionCode + '-' + variant.buildType.name + '.apk'
                output.outputFile = new File(oldFile.parent, releaseApkName)
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    testCompile 'junit:junit:4.12'

    /* Android 万能的Adapter for ListView,RecyclerView,GridView等，支持多种Item类型的情况。 */
    compile 'com.zhy:base-rvadapter:3.0.3'
    //* Rxjava——网络 */
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    /* 图片轮播图 */
    compile 'com.bigkoo:convenientbanner:2.0.5'
    /* Butter Knife */
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    /* RoundedImageView */
    compile 'com.makeramen:roundedimageview:2.2.1'
    /* fragmention */
    compile 'me.yokeyword:fragmentation:1.1.6'
    /*     glide    */
    compile 'com.github.bumptech.glide:glide:3.7.0'
    /*      魅族式轮播图      */
    compile 'com.github.pinguo-zhouwei:MZBannerView:v2.0.2'
    /*    jcVideo   */
    compile 'cn.jzvd:jiaozivideoplayer:6.2.10'
    /* Android-导航栏特效，主要是导航栏字体大小和颜色的渐变特效 可以是固定的几个,可也可以是水平滚动 */
    compile('com.gxz.pagerslidingtabstrip:library:1.3') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
}
